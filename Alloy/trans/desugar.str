module trans/desugar

imports // signatures
	
	src-gen/signatures/Alloy-sig
	
imports // functions
	
	trans/api/alloy
	trans/lib/desugar	
	trans/lib/origin
	
rules
	
	desugar-before-analysis = 
		module-sig-names-to-sigs-name;
		try(sometd(sigdecl-normalize-fields));
		try(sometd(preddecl-funparam-names-to-funparams-name));
		warning-on-missing-origin
	
	desugar-after-analysis = 
		id

rules // sig A,B{} -> sig A{} sig B{}

	module-sig-names-to-sigs-name = 
		normalize-comma-separated(
			module-get-paragraphs, module-set-paragraphs, 
			sigdecl-get-names, sigdecl-set-names)

rules // sig A{b1,b2:B} -> sig A{b1:B, b2:B}

	sigdecl-normalize-fields =
		normalize-comma-separated(
			sigdecl-get-fields, sigdecl-set-fields, 
			fielddecl-get-names, fielddecl-set-names)

rules // pred a (a1,a2:A){} -> pred a (a1:A, a2:A){}

	preddecl-funparam-names-to-funparams-name = 
		normalize-comma-separated(
			preddecl-get-funparams, preddecl-set-funparams, 
			funparam-get-names, funparam-set-names)
